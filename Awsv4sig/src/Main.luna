import Std.Base
import Std.System
import Std.Foreign
import Std.Foreign.C.Value
import Std.HTTP
import Std.Time


def main:
    awsAccessKey = System.getEnv "AWS_ACCESS_KEY"
    awsSecretKey = System.getEnv "AWS_SECRET_KEY"
    amzdate = Time . now . toUTC . format "%Y%m%dT%H%M%SZ"
    httpMethod = "GET"
    canonUri = "/2015-03-31/functions"
    canonQuery = ""
    host = "lambda.eu-west-2.amazonaws.com"
    body = ""
    region = "eu-west-2"
    service = "lambda"

    args = [awsAccessKey, awsSecretKey, amzdate, httpMethod, canonUri, canonQuery, host, body, region, service]
    cargs = args . map (str: (CString.fromText str).toCArg)

    funPtr = lookupSymbol "awsv4sig" "awsv4sig_auth_header"
    authHeader = funPtr . call CString cargs

    request = Http.get ("https://" + host + canonUri) . addHeader "X-Amz-Date" amzdate . addHeader "Authorization" authHeader.toText
    print request

    response = request . perform . json
    print response

    authHeader.free
